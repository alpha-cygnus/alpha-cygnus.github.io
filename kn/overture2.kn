Note2Detune {
	PIN -> {note|(note - 69)*100} -> P2A -> AOUT
}

Note2Freq {
	PIN -> {note|pow(2, (note - 69)/12)*440} -> P2A -> AOUT
}

MainOut {
	AIN inp -> Dest dest, AOUT out
}

Echo {
	AIN inp -> Delay d -> * gwet -> inp -> * gdry -> AOUT out
	AIN<0.5> dry -> [gain]gdry
	AIN<0.5> wet -> [gain]gwet
	AIN<0.5> time -> [time]d
}

PingPong {
	AIN inp -> * gdecayIL -> Delay dl -> * gdecayLR -> Delay dr -> * gdecayRL -> dl
	inp -> * gdry -> AOUT out
	dl -> Pan pl -> out
	dr -> Pan pr -> out
	AIN<0.7> spread -> [pan]pr
	spread -> *-1 -> [pan]pl

	AIN<0.5> dry -> [gain]gdry
	AIN<0.3> decay -> [gain] gdecayLR, gdecayRL, gdecayIL
	AIN<0.4> time ->[time] dl, dr
}

TestSynth {
	MIDIIN inp -> Midi2Trigger trig, Midi2Note<LAST> note
	note -> Note2Detune ->[detune] Osc<SAW> o1, o2, o3, Osc<SQUARE> sub
	o1, o2, o3 -> Filter<HP> flt -> ADSR<0.01, 0.3, 0.5, 1> ampEnv -> AOUT out
	sub -> ampEnv
	ADSR<0.8, 0.3, 0, 0.5> fltEnv -> *10000 -> +30 -> [freq]flt
	trig ->[trigger] ampEnv, fltEnv
	+15 ->[detune] o1
	-15 ->[detune] o2
	-2400 ->[detune] sub
}

SpreadingLead {
	MIDIIN inp -> Midi2Trigger trig, Midi2Note<LAST> note
	note -> Note2Detune ->[detune] Osc<SAW> o1, o2, o3, o4, o5, Osc<SQUARE> sub
	o1, o2, o3, o4, o5 -> ADSR<0.1, 0.3, 0.7, 1> ampEnv -> *0.2 -> AOUT out
	trig ->([trigger]ampEnv; [trigger]ADSR<5, 0.1, 1, 0.3> -> Delay<0.3> sEnv)
	AIN<15> spread -> [gain]* gspread
	#sEnv -> gspread -> [detune] o2
	#sEnv -> gspread -> *-1 -> [detune] o3
	#sEnv -> gspread -> * 2 -> [detune] o4
	#sEnv -> gspread -> *-3 -> [detune] o5
	sEnv -> gspread -> ([detune] o2; *-1 -> [detune]o3; *2 -> [detune]o4; *-3 -> [detune]o5)
	-1200 ->[detune] sub
}

FM1 {
	MIDIIN inp -> Midi2Trigger trig, Midi2Note<LAST> note
	note -> Note2Detune ->[detune] Osc<SINE> o1, o2
	o2 -> *100 fmDetune ->[detune] o1
	trig ->[trigger] ADSR<5, 0.1, 1, 0.3> -> *500 ->[gain]fmDetune
	100 ->[gain] fmDetune
	-1800 ->[detune] o2
	0.5 -> [freq]Osc -> *100 ->[detune] o2
	o1 -> ADSR<0.1, 0.3, 0.7, 1> amp -> AOUT out
	trig ->[trigger] amp
}

BD1 {
	MIDIIN inp -> Midi2Trigger<RETRIG> trig, Midi2Note<LAST> note
	trig->[trigger] ADSR<0.001, 0.09, 0.05, 0.3> freqEnv, ADSR<0.001, 0.2, 0.8, 0.1> ampEnv
	freqEnv -> *1000 ->[freq] Osc<SINE> osc -> ampEnv -> *0.5 -> AOUT out
	note -> Note2Detune ->[detune] osc
}

HH1 {
	MIDIIN inp -> Midi2Trigger<RETRIG> trig, Midi2Note<LAST> note
	trig -> [trigger]ADSR<0.01, 0.5, 0.001, 0.1> ampEnv
	Noise osc -> Filter<HP> flt 
		-> ampEnv -> *(0.5) -> AOUT out

	note -> Note2Detune ->[detune] flt
	4800 ->[detune]flt
}

#Keyboard theKeyboard -> TestSynth ts -> Echo -> *(0.1) -> Pan pan -> MainOut theMainOut

Keyboard theKeyboard
	#-> SpreadingLead ts 
	#-> FM1 #, SpreadingLead
	#-> BD1
	-> HH1
	#-> PingPong 
	-> *0.1 -> MainOut theMainOut

#{*v = 1 | v < 0 ? v : 0 }
#{*}

#0.3 -> [freq]Osc panLfo -> *0.5 -> [pan]pan
#theKeyboard -> Midi2Trigger -> [trigger]panLfo


